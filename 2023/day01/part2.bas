100 PRINT "ENTER FILENAME: INPUT.SEQ";
110 PRINT RPT$(157,9);:
120 OPEN 1,0:INPUT#1,FI$:CLOSE 1:PRINT:TI=0
130 OPEN 1,8,2,FI$
140 OPEN 15,8,15:INPUT#15,DS,DS$,T,S:CLOSE 15
150 IF DS THEN PRINT DS$:GOTO 100
160 DIM D$(9):FOR D=0 TO 9:READ D$(D):NEXT
170 DATA ZERO,ONE,TWO,THREE,FOUR,FIVE,SIX,SEVEN,EIGHT,NINE
180 FOR EF=0 TO 1 STEP 0
190 : LINPUT#1,LI$
200 : EF=(ST AND 64) / 64
210 : F=-1:L=-1
220 : FOR I=1 TO LEN(LI$)
230 :   D$=MID$(LI$,I,1)
240 :   IF D$>="0" AND D$<="9" THEN F=VAL(D$):I=LEN(LI$):GOTO 280
250 :   FOR D=0 TO 9
260 :     IF MID$(LI$,I,LEN(D$(D))) = D$(D) THEN F=D:D=9:I=LEN(LI$)
270 :   NEXT D
280 : NEXT I
290 : FOR I=LEN(LI$) TO 1 STEP -1
300 :   D$=MID$(LI$,I,1)
310 :   IF D$>="0" AND D$<="9" THEN L=VAL(D$):I=1:GOTO 360
320 :   FOR D=0 TO 9
330 :     IF I-LEN(D$(D)) < 0 THEN 350
340 :     IF MID$(LI$,I-LEN(D$(D))+1,LEN(D$(D))) = D$(D) THEN L=D:D=9:I=1
350 :   NEXT D
360 : NEXT I
365 : REM ?LI$":"F,L
370 : T=T+F*10+L
380 NEXT EF
390 CLOSE 1
400 PRINT T
410 PRINT TI
