GETFILE:
  PRINT "CHOOSE INPUT FILE ([1]=SAMPLE.SEQ [2]=INPUT.SEQ):";
GETKEY:
  GET KEY$: IF KEY$="" THEN GETKEY
  IF KEY$<>"1" AND KEY$<>"2" AND KEY$<>"S" AND KEY$<>"I" AND KEY$<>"Q" THEN GETKEY
  IF KEY$="Q" THEN PRINT "QUIT": END
  IF KEY$="1" OR KEY$="S" THEN FILE$="SAMPLE.SEQ":GOTO GOTFILE
  FILE$="INPUT.SEQ"
GOTFILE:
  PRINT FILE$
  OPEN 1,8,2,FILE$
  OPEN 15,8,15:INPUT#15,DS,DS$,T,S:CLOSE 15
  IF DS THEN CLOSE 1:PRINT DS$:GOTO GETFILE

  DIGIT=0:PART=1
START:
  DIM ROW$(1),COUNTED$(1):CUR=0
  TOTAL = 0
  FOR EOF=0 TO 1 STEP 0
    LINPUT#1, ROW$(CUR)
    EOF = (ST AND 64) / 64
    PREV = 1 - CUR
    REM HAVEN'T COUNTED ANY OF THIS YET
    COUNTED$(CUR) = RPT$(0, LEN(ROW$(CUR)))
    FOR I=1 TO LEN(ROW$(CUR))
      CH$=MID$(ROW$(CUR),I,1)
      IF CH$="." THEN NEXT.CHAR
      IF CH$<"0" OR CH$ >"9" THEN CHECK.PART
      REM FOUND A DIGIT
      COUNTED = ASC(MID$(COUNTED$(CUR), I))
      REM IF IT'S UNCOUNTED, CHECK FOR PARTS AROUND IT
      IF COUNTED = 0 THEN GOSUB SCAN.NEIGHBORS
      GOTO NEXT.CHAR
CHECK.PART:
      REM FOUND A PART; CHECK FOR UNCOUNTED DIGITS AROUND IT
      GOSUB SCAN.NEIGHBORS
NEXT.CHAR:
    NEXT I
    CUR = 1 - CUR
  NEXT EOF
  PRINT "TOTAL:"TOTAL
  END

SCAN.NEIGHBORS:
  REM FOUND EITHER AN UNCOUNTED DIGIT OR A PART
  REM SCAN NEIGHBORS TO SEE IF WE NEED TO ADD A PART NUMBER
  TARGET=DIGIT: IF CH$>="0" AND CH$<="9" THEN X=I:Y=CUR:TARGET=PART
  DO.VERTICAL = LEN(ROW$(PREV)) > 0: REM TRUE AFTER FIRST LINE
  FOR DX = -1 TO 1
    NI = I + DX
    IF NI < 1 OR NI > LEN(ROW$(CUR)) THEN NEXT.NEIGHBOR
    FOR DY=DO.VERTICAL TO 0
      IF NOT (DX OR DY) THEN NEXT.LINE
      ROW = ABS(CUR + DY)
      DIGIT$ = MID$(ROW$(ROW), NI, 1)
      IF DIGIT$="." THEN NEXT.LINE
      COUNTED = ASC(MID$(COUNTED$(ROW), NI))
      IF TARGET=DIGIT AND COUNTED THEN NEXT.LINE
      IF TARGET=DIGIT AND DIGIT$>="0" AND DIGIT$<="9" THEN GOT.DIGIT
      IF TARGET=PART AND  DIGIT$>="0" AND DIGIT$<="9" THEN NEXT.LINE

      REM FOUND A PART; THE DIGIT SHOULD COUNT.
      GOSUB ADD.PART
      REM AND WE'RE DONE
      DY=0:DX=1:GOTO NEXT.LINE
GOT.DIGIT:
     X=NI:Y=ROW:GOSUB ADD.PART
NEXT.LINE:
    NEXT DY
NEXT.NEIGHBOR:
  NEXT DX
  RETURN

ADD.PART:
  REM FOUND A DIGIT THAT WE SHOULD COUNT. FIND THE NUMBER CONTAINING IT AND ADD IT TO THE TOTAL
  LEFT = X
  RIGHT = X
  FIRST = 0
  LAST = 0
  FOR DONE = 0 TO -1 STEP 0
    IF FIRST THEN DO.RIGHT
    IF LEFT=1 THEN FIRST=LEFT: GOTO DO.RIGHT
    DIGIT$ = MID$(ROW$(Y), LEFT-1, 1)
    IF DIGIT$ < "0" OR DIGIT$ > "9" THEN FIRST=LEFT:GOTO DO.RIGHT
    LEFT = LEFT - 1
DO.RIGHT:
    IF LAST THEN NEXT.SCAN
    IF RIGHT=LEN(ROW$(Y)) THEN LAST=RIGHT: GOTO NEXT.SCAN
    DIGIT$ = MID$(ROW$(Y), RIGHT+1, 1)
    IF DIGIT$ < "0" OR DIGIT$ > "9" THEN LAST=RIGHT:GOTO NEXT.SCAN
    RIGHT = RIGHT + 1
NEXT.SCAN:
    DONE = (FIRST > 0)  AND (LAST > 0)
  NEXT DONE
  SIZE = LAST - FIRST + 1
  IF ASC(MID$(COUNTED$(Y),FIRST)) THEN PRINT "ALREADY ADDED"VALUE:GOTO SKIP.ADD
  VALUE = VAL(MID$(ROW$(Y),FIRST,SIZE))
  TOTAL = TOTAL + VALUE
SKIP.ADD:
  COUNTED$(Y) = LEFT$(COUNTED$(Y), FIRST - 1) + RPT$(1, SIZE) + MID$(COUNTED$(Y), LAST + 1)
  RETURN
