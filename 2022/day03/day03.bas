100 DIM R$(2):REM RUCKSACKS WITHIN GROUP
110 REM PRIORITY OF ITEM 
120 DEF FNP(L) = (L AND 31) - 26*(L < 96)
130 REM MOD 3
140 DEF FNM3(N) = N - 3 * INT(N/3)
150 PRINT:FI$="":INPUT "FILENAME";FI$
160 IF FI$="" THEN PRINT "NEVER MIND.": END
170 OPEN 1,8,2,FI$
180 GOSUB 600: IF DS=0 THEN 200
190 CLOSE 1:PRINT "COULD NOT OPEN FILE.":GOTO 150
200 TI=0: REM RESET TIMER
205 P1=0: REM PART 1 TOTAL
210 P2=0: REM PART 2 TOTAL
220 LI$="":LN=0: REM CURRENT LINE AND NUMBER
230 FOR D=0 TO 1 STEP 0:REM WHILE NOT DONE
240 : GET#1, BY$:IF ST AND 64 THEN D=1
250 : BY=ASC(BY$):IF BY=10 THEN 270
260 : LI$=LI$+BY$:GOTO 490
270 : L=LEN(LI$):H=L/2
280 : REM PART 1: SEARCH FOR COMMON ITEM BETWEEN COMPARTMENTS
290 : FOR I=1 TO H
300 :   FOR J=H+1 TO L
310 :     IF MID$(LI$,I,1) <> MID$(LI$,J,1) THEN 340
320 :     P1 = P1 + FNP(ASC(MID$(LI$,I)))
330 :     J = L :I = H
340 :   NEXT J
350 : NEXT I
360 : REM PART 2: SEARCH FOR COMMON ITEM AMONG RUCKSACKS
370 : G=FNM3(LN):R$(G)=LI$:LN=LN+1:LI$=""
380 : IF G < 2 THEN 490
390 : FOR I=1 TO LEN(R$(0))
400 :   FOR J=1 TO LEN(R$(1))
410 :     IF MID$(R$(0),I,1) <> MID$(R$(1),J,1) THEN 470
420 :     FOR K=1 TO LEN(R$(2))
430 :       IF MID$(R$(0),I,1) <> MID$(R$(2),K,1) THEN 460
440 :       P2 = P2 + FNP(ASC(MID$(R$(0),I)))
450 :       I=LEN(R$(0)):J=LEN(R$(1)):K=LEN(R$(2))
460 :     NEXT K
470 :   NEXT J
480 : NEXT I
490 NEXT D
500 CLOSE 1:T=TI/60
505 PRINT "PROCESSED"LN"LINES IN"T"SECONDS. SCORES:"
510 PRINT "PART 1:"P1
520 PRINT "PART 2:"P2
530 GOTO 150
600 OPEN 15,8,15:INPUT#15,DS,DS$,T,S:CLOSE 15:RETURN
