100 DEF FNM3(N)=N-3*INT(N/3):REM MOD 3
110 PRINT:FI$="":INPUT "FILENAME";FI$
120 IF FI$="" THEN PRINT "NEVER MIND.": END
130 OPEN 1,8,2,FI$
140 GOSUB 500: IF DS=0 THEN 160
150 CLOSE 1:PRINT "COULD NOT OPEN FILE.":GOTO 110
160 P1=0: REM PART 1 TOTAL
170 P2=0: REM PART 1 TOTAL
180 FOR D=0 TO 1 STEP 0:REM WHILE NOT DONE
190 : GET#1, BY$:IF ST AND 64 THEN D=1
200 : BY=ASC(BY$)
210 : IF (BY>=ASC("A")) AND (BY<=ASC("C")) THEN E=BY-ASC("A")
220 : IF (BY<ASC("X")) OR (BY>ASC("Z")) THEN 260
230 : H=BY-ASC("X")
235 : REM SEE NOTES.TXT FOR DERIVATION
240 : P1=P1+3*FNM3(H-E+1)+H+1
250 : P2=P2+3*H+FNM3(E+H+2)+1
260 NEXT D
270 CLOSE 1
280 PRINT "PART 1:"P1
290 PRINT "PART 2:"P2
300 GOTO 110
500 OPEN 15,8,15:INPUT#15,DS,DS$,T,S:CLOSE 15:RETURN
