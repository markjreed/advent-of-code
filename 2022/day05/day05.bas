100 DIM S$(1,15,63), H(1,15), N(511),F(511),T(511):NM=0
110 PRINT:FI$="":INPUT "FILENAME";FI$
120 IF FI$="" THEN PRINT "NEVER MIND.": END
130 OPEN 1,8,2,FI$
140 GOSUB 700: IF DS=0 THEN 160
150 CLOSE 1:PRINT "COULD NOT OPEN FILE.":GOTO 110
160 FOR E=0 TO 1 STEP 0:REM WHILE NOT EOF
170 : GOSUB 800:IF ST AND 64 THEN E=1
180 : IF L THEN 290
190 : IF VAL(LI$) THEN L=1:GOSUB 880:GOTO 370:REM LABEL LINE
200 : REM PART STACK PICTURE, PARSE
210 : S=0:FOR I=2 TO LEN(LI$) STEP 4
220 :   C$=MID$(LI$,I,1):IF C$=" " THEN 260
230 :   H(0,S)=H(0,S)+1
240 :   FOR J=H(0,S) TO 1 STEP-1:S$(0,S,J)=S$(0,S,J-1):NEXT J
250 :   S$(0,S,0)=C$
260 :   S=S+1:IF S>NS THEN NS=S
270 : NEXT I
280 : GOTO 370
290 : REM MOVE INSTRUCTION, PARSE
300 : IF LEFT$(LI$,4)<>"move" THEN PRINT LEFT$(LI$,$):GOTO 370
310 : N(NM)=VAL(MID$(LI$,6))
320 : I=13+INT(LOG(N(NM))/LOG(10))
330 : F(NM)=VAL(MID$(LI$,I))-1
340 : I=I+5+INT(LOG(F(NM)+1)/LOG(10))
350 : T(NM)=VAL(MID$(LI$,I))-1
360 : NM=NM+1
370 NEXT E
460 FOR I=0 TO 1
470 : PRINT "PART "I+1": ";
480 : FOR M=0 TO NM-1
490 :   IF I THEN 560
500 :   FOR J=1 TO N(M)
510 :     H(I,F(M))=H(I,F(M))-1
520 :     S$(I, T(M), H(I,T(M))) = S$(I,F(M),H(I,F(M)))
530 :     H(I,T(M))=H(I,T(M))+1
540 :   NEXT J
550 :   GOTO 610
560 :   H(I,F(M))=H(I,F(M))-N(M)
570 :   FOR J=0 TO N(M)-1
580 :     S$(I,T(M),H(I,T(M))+J) = S$(I,F(M),H(I,F(M))+J)
590 :   NEXT J
600 :   H(I,T(M))=H(I,T(M))+N(M)
610 : NEXT M
630 : FOR S=0 TO NS-1:PRINT S$(I,S,H(I,S)-1);:NEXT:PRINT
640 NEXT I
650 END
700 OPEN 15,8,15:INPUT#15,DS,DS$,T,S:CLOSE 15:RETURN
800 REM INPUT LINE WITH UNIX LINE FEEDS
810 LI$=""
820 GET#1,BY$:LE = ST AND 64
830 IF LEN(BY$)=0 THEN RETURN
840 BY=ASC(BY$):IF BY=10 THEN RETURN
850 IF (BY>96) AND (BY<192) THEN BY=BY+96
860 LI$=LI$+CHR$(BY):IF LE THEN RETURN
870 GOTO 820
880 REM SAVE COPY OF STARTING CONFIG AS IT IS PRINTED
890 PRINT "STARTING CONFIG:"
900 FOR S=0 TO NS-1
910 : PRINT S+1":";:H(1,S)=H(0,S)
920 : FOR I=0 TO H(0,S)-1
930 :  S$(1,S,I)=S$(0,S,I):PRINT " ["S$(0,S,I)"]";
940 : NEXT I:PRINT
950 NEXT S
960 RETURN
