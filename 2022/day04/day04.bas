100 FOR D=0 TO 1 STEP 0:REM WHILE NOT DONE
110 : PRINT:FI$="":INPUT "FILENAME";FI$
120 : IF FI$="" THEN PRINT "NEVER MIND.": END
130 : OPEN 1,8,2,FI$
140 : GOSUB 500: IF DS=0 THEN 160
150 : CLOSE 1:PRINT "COULD NOT OPEN FILE.":GOTO 110
160 : P1=0: REM PART 1 TOTAL
170 : P2=0: REM PART 2 TOTAL
180 : FOR E=0 TO 1 STEP 0:REM WHILE NOT EOF
190 :   GOSUB 400:IF ST AND 64 THEN E=1:REM INPUT#1,LI$ WITH UNIX LF
200 :   LL=VAL(LI$):LH=0:RL=0:RH=0
210 :   FOR I=2 TO LEN(LI$)
220 :     IF MID$(LI$,I,1)<>"-" THEN 250
230 :     H=VAL(MID$(LI$,I+1)): IF LH=0 THEN LH=H:GOTO 250
240 :     RH=H
250 :     IF MID$(LI$,I,1)="," THEN RL=VAL(MID$(LI$,I+1))
260 :   NEXT I
270 :   P1=P1-(((LL <= RL) AND (LH >= RH)) OR ((RL <= LL) AND (RH >= LH)))
280 :   I=((LL>=RL) AND (LL<=RH)) OR ((LH>=RL) AND (LH<=RH))
290 :   I=I OR ((RL>=LL) AND (RL<=LH)) OR ((RH>=LL) AND (RH<=LH))
300 :   P2=P2-I
310 : NEXT E
320 : CLOSE 1
330 : PRINT "PART 1:"P1
340 : PRINT "PART 2:"P2
350 NEXT D
360 END
400 REM INPUT LINE WITH UNIX LINE FEEDS
410 LI$=""
420 GET#1,BY$:LE = ST AND 64
430 IF LEN(BY$)=0 THEN RETURN
440 IF ASC(BY$)=10 THEN RETURN
450 LI$=LI$+BY$:IF LE THEN RETURN
460 GOTO 420
500 OPEN 15,8,15:INPUT#15,DS,DS$,T,S:CLOSE 15:RETURN
