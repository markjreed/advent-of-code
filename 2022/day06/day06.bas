100 DIM W$(1),P(1),M(1):L$=CHR$(157):U$=CHR$(145)
110 FOR D=0 TO 1 STEP 0:REM WHILE NOT DONE
120 : PRINT:FI$="":INPUT "FILENAME";FI$
130 : IF FI$="" THEN PRINT "NEVER MIND.": END
140 : OPEN 1,8,2,FI$
150 : GOSUB 500: IF DS=0 THEN 170
160 : CLOSE 1:PRINT "COULD NOT OPEN FILE.":GOTO 120
170 : FOR I=0 TO 1:M(I)=0:P(I)=0:W$(I)="":NEXT I
180 : FOR E=0 TO 1 STEP 0:REM WHILE NOT EOF
190 :   GET#1,BY$:IF ST AND 64 THEN E=1
200 :   IF M(0) AND M(1) AND (E=0) AND (BY$<>CHR$(10)) THEN 190
210 :   IF BY$<>CHR$(10) THEN 230
220 :   FOR I=0 TO 1:M(I)=0:P(I)=0:W$(I)="":NEXT I:GOTO 190
230 :   IF M(0) AND M(1) THEN 360
240 :   FOR P=0 TO 1:REM PART #
250 :     IF M(P) THEN 350
260 :     S=4+10*P
270 :     P(P)=P(P)+1:W$(P)=W$(P)+BY$:IF LEN(W$(P))>S THEN W$(P)=MID$(W$(P),2)
280 :     IF LEN(W$(P)) < S THEN 350
290 :     FOR I=1 TO S-1
300 :       M(P)=P(P):A$=MID$(W$(P),I,1)
310 :       FOR J=I+1 TO S
320 :         IF MID$(W$(P),J,1) = A$ THEN M(P)=0:J=S:I=S-1
330 :       NEXT J
340 :     NEXT I
350 :   NEXT P
360 :   IF M(0) AND M(1) THEN PRINT "PART 1:"M(0),"PART 2:"M(1)
370 : NEXT E
380 : CLOSE 1
390 NEXT D
400 END
490 REM CHECK DISK STATUS
500 OPEN 15,8,15:INPUT#15,DS,DS$,T,S:CLOSE 15:RETURN
