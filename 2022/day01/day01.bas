100 DIM MC(2):REM MAX CALORIES
110 PRINT:FI$="":INPUT "FILENAME";FI$
120 IF FI$="" THEN PRINT "NEVER MIND": END
130 OPEN 1,8,2,FI$
140 GOSUB 500: IF DS=0 THEN 160
150 CLOSE 1:PRINT "COULD NOT OPEN FILE.":GOTO 110
160 REM CUR SNACK, CUR CAL, TOP 3
170 CS=0: CC=0: MC(0)=0: MC(1)=0: MC(2)=0
180 FOR D=0 TO 1 STEP 0:REM WHILE NOT DONE
190 : GET#1, BY$:IF ST AND 64 THEN D=1:GOTO 330
200 : BY=ASC(BY$)
210 : IF BY=10 THEN 250:REM END OF LINE
220 : IF (BY>=48) AND (BY<=57) THEN 240
230 : PRINT "UNEXPECTED CHARACTER '"BY$"'. ABORT.":CLOSE1:END
240 : CS=CS*10+BY-48:GOTO 330:REM ADD DIGIT TO CURRENT SNACK
250 : CC=CC+CS:CS=0
260 : IF PB<>10 THEN 330
270 : REM END OF ELF
280 : IF CC < MC(2) THEN 320
290 : MC(2)=CC: IF CC < MC(1) THEN 320
300 : MC(2)=MC(1):MC(1)=CC: IF CC < MC(0) THEN 320
310 : MC(1)=MC(0):MC(0)=CC
320 : CC=0
330 : PB=BY
340 NEXT D
350 CLOSE 1
360 PRINT "MAX CAL PER ELF:"MC(0)
370 S=0:FORI=0TO2:S=S+MC(I):NEXTI
380 PRINT "TOP 3 ELVES TOTAL:"S
390 GOTO 110
500 OPEN 15,8,15:INPUT#15,DS,DS$,T,S:CLOSE 15:RETURN
